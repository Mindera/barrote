apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'
apply plugin: "org.sonarqube"

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    testBuildType "qa"

    defaultConfig {

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

    }

    buildTypes {

        debug {
            matchingFallbacks = ['debug']
        }

        qa {
            matchingFallbacks = ['debug']
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true

        // if true, stop the gradle build if errors are found
        abortOnError false

        // do not ignore warnings
        warningsAsErrors true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    configurations.all {
        resolutionStrategy {
            resolutionStrategy.force "com.android.support:support-annotations:$supportLibraryVersion"
            // annotation processor conflicts
            resolutionStrategy.force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
            // lint conflicts
            resolutionStrategy.force "org.apache.httpcomponents:httpcore:$httpComponentsVersion"
            resolutionStrategy.force "com.google.guava:guava:$guavaVersion"
            // powermock
            resolutionStrategy.force "org.objenesis:objenesis:2.5"

            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()
        }
    }

    configurations.all {
        resolutionStrategy.force "org.codehaus.plexus:plexus-utils:1.5.15"
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlinVersion"

    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
    implementation("io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion") {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
    }

    testImplementation "org.jetbrains.kotlin:kotlin-test:$rootProject.kotlinVersion"
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation "org.powermock:powermock-module-junit4:$rootProject.powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$rootProject.powermockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$rootProject.powermockVersion"
    testImplementation "org.powermock:powermock-classloading-xstream:$rootProject.powermockVersion"
    testImplementation("org.robolectric:robolectric:$rootProject.robolectricVersion") {
        exclude group: 'com.thoughtworks.xstream', module: 'xstream'
    }
}

jacoco {
    toolVersion "0.8.1"
}

//Unit Test Coverage filtered - Note: This is currently NOT being used on the Codacy report. This is to run locally if needed
task createModuleSkeletoidUnitTestReport(type: JacocoReport, dependsOn: [':base:testQaUnitTest']) {

    reports {
        html.enabled = true
    }

    def fileFilter = [
            //Android stuff
            '**/R.class',
            '**/BR.class',
            '**/R$*.class',
            '**/BR$*.class',
            '**/BuildConfig.*',
            'android/**/*.*',
            '**/Manifest*.*',
            //Data Binding
            '**/*databinding/**/*.*',
            //Test
            '**/**Test*.*']

    //To support Java coverage on Unit tests
    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/qa/compileQaJavaWithJavac/classes/qa", excludes: fileFilter)
    //To support Kotlin coverage on Unit tests
    def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/qa", excludes: fileFilter)

    def mainSrc = "${project.projectDir}/src/main/java"
    def debugSrc = "${project.projectDir}/src/debug/java"

    sourceDirectories = files([mainSrc, debugSrc])

    classDirectories = files([debugTree], [kotlinDebugTree])

    executionData = files("${buildDir}/jacoco/testQaUnitTest.exec")
}

//Unit Test Coverage filtered - Note: This is currently NOT being used on the Codacy report. This is to run locally if needed
task createGlobalSkeletoidUnitTestReport(type: JacocoReport, dependsOn: [':analytics-firebase:createModuleSkeletoidUnitTestReport',
                                                                         ':analytics-ga:createModuleSkeletoidUnitTestReport',
                                                                         ':base:createModuleSkeletoidUnitTestReport',
                                                                         ':kt-extensions:createModuleSkeletoidUnitTestReport',
                                                                         ':performance-firebase:createModuleSkeletoidUnitTestReport',
                                                                         ':rxjava:createModuleSkeletoidUnitTestReport',
                                                                         ':rxbindings:createModuleSkeletoidUnitTestReport'
]) {


    reports {
        html.enabled = true
    }

    def fileFilter = [
            //Android stuff
            '**/R.class',
            '**/BR.class',
            '**/R$*.class',
            '**/BR$*.class',
            '**/BuildConfig.*',
            'android/**/*.*',
            '**/Manifest*.*',
            //Data Binding
            '**/*databinding/**/*.*',
            //Test
            '**/**Test*.*']

    def analyticsFbDebugTree = fileTree(dir: "../analytics-firebase/build/intermediates/javac/qa/compileQaJavaWithJavac/classes/com/mindera/skeletoid", excludes: fileFilter)
    def analyticsFbKotlinDebugTree = fileTree(dir: "../analytics-firebase/build/tmp/kotlin-classes/qa/com/mindera/skeletoid", excludes: fileFilter)

    def analyticsGaDebugTree = fileTree(dir: "../analytics-ga/build/intermediates/javac/qa/compileQaJavaWithJavac/classes/com/mindera/skeletoid", excludes: fileFilter)
    def analyticsGaKotlinDebugTree = fileTree(dir: "../analytics-ga/build/tmp/kotlin-classes/qa/com/mindera/skeletoid", excludes: fileFilter)

    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/qa/compileQaJavaWithJavac/classes/com/mindera/skeletoid", excludes: fileFilter)
    def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/qa/com/mindera/skeletoid", excludes: fileFilter)

    def ktExtensionsDebugTree = fileTree(dir: "../kt-extensions/build/intermediates/javac/qa/compileQaJavaWithJavac/classes/com/mindera/skeletoid", excludes: fileFilter)
    def ktExtensionsKotlinDebugTree = fileTree(dir: "../kt-extensions/build/tmp/kotlin-classes/qa/com/mindera/skeletoid", excludes: fileFilter)

    def performanceFbDebugTree = fileTree(dir: "../performance-firebase/build/intermediates/javac/qa/compileQaJavaWithJavac/classes/com/mindera/skeletoid", excludes: fileFilter)
    def performanceFbKotlinDebugTree = fileTree(dir: "../performance-firebase/build/tmp/kotlin-classes/qa/com/mindera/skeletoid", excludes: fileFilter)

    def rxBindingsDebugTree = fileTree(dir: "../rxbindings/build/intermediates/javac/qa/compileQaJavaWithJavac/classes/com/mindera/skeletoid", excludes: fileFilter)
    def rxBindingsKotlinDebugTree = fileTree(dir: "../rxbindings/build/tmp/kotlin-classes/qa/com/mindera/skeletoid", excludes: fileFilter)

    def rxJavaDebugTree = fileTree(dir: "../rxjava/build/intermediates/javac/qa/compileQaJavaWithJavac/classes/com/mindera/skeletoid", excludes: fileFilter)
    def rxJavaKotlinDebugTree = fileTree(dir: "../rxjava/build/tmp/kotlin-classes/qa/com/mindera/skeletoid", excludes: fileFilter)

    def analyticsFbSrc = "../analytics-firebase/src/main/java"
    def analyticsFbDebugSrc = "../analytics-firebase/src/debug/java"

    def analyticsGaSrc = "../analytics-ga/src/main/java"
    def analyticsGaDebugSrc = "../analytics-ga/src/debug/java"

    def mainSrc = "${project.projectDir}/src/main/java"
    def debugSrc = "${project.projectDir}/src/debug/java"

    def ktExtensionsSrc = "../kt-extensions/src/main/java"
    def ktExtensionsDebugSrc = "../kt-extensions/src/debug/java"

    def performanceFbSrc = "../performance-firebase/src/main/java"
    def performanceFbDebugSrc = "../performance-firebase/src/debug/java"

    def rxBindingsSrc = "../rxbindings/src/main/java"
    def rxBindingsDebugSrc = "../rxbindings/src/debug/java"

    def rxJavaSrc = "../rxjava/src/main/java"
    def rxJavaDebugSrc = "../rxjava/src/debug/java"

    sourceDirectories = files([mainSrc, debugSrc, analyticsFbSrc, analyticsFbDebugSrc,
                               analyticsGaSrc, analyticsGaDebugSrc, ktExtensionsSrc, ktExtensionsDebugSrc,
                               performanceFbSrc, performanceFbDebugSrc, rxBindingsSrc, rxBindingsDebugSrc,
                               rxJavaSrc, rxJavaDebugSrc])


    classDirectories = files([debugTree], [kotlinDebugTree, analyticsFbDebugTree, analyticsFbKotlinDebugTree,
                                           analyticsGaDebugTree, analyticsGaKotlinDebugTree, ktExtensionsDebugTree, ktExtensionsKotlinDebugTree,
                                           performanceFbDebugTree, performanceFbKotlinDebugTree, rxBindingsDebugTree, rxBindingsKotlinDebugTree,
                                           rxJavaDebugTree, rxJavaKotlinDebugTree])

    executionData = files("${buildDir}/jacoco/testQaUnitTest.exec",
            "../analytics-firebase/build/jacoco/testQaUnitTest.exec",
            "../analytics-firebase/build/jacoco/testQaUnitTest.exec",
            "../analytics-ga/build/jacoco/testQaUnitTest.exec",
            "../kt-extensions/build/jacoco/testQaUnitTest.exec",
            "../performance-firebase/build/jacoco/testQaUnitTest.exec",
            "../rxbindings/build/jacoco/testQaUnitTest.exec",
            "../rxjava/build/jacoco/testQaUnitTest.exec")
}

tasks['sonarqube'].with {
    dependsOn.clear()
    dependsOn createGlobalSkeletoidUnitTestReport
}

sonarqube {
    properties {

        property "sonar.sources", "src"
        property "sonar.exclusions", "**/*test*/**,build/**,*.iml,**/*generated*,**extensions**"
        property "sonar.tests", "./src/test/"
        property "sonar.test.inclusions", "**/*test*/**"
        property "sonar.import_unknown_files", true

        property "sonar.java.binaries", "${buildDir}/intermediates/javac/qa/compileQaJavaWithJavac/classes,${buildDir}/tmp/kotlin-classes/qaUnitTest"
        property "sonar.java.source", "base/src/main/java"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.jacoco.reportPaths", fileTree(dir: "${buildDir}/jacoco/", includes: ["**/*.exec"])
        property "sonar.jacoco.itReportPath", fileTree(dir: "${buildDir}/outputs/code-coverage/connected/", includes: ["**/*.ec"])
        property "sonar.android.lint.report", "${buildDir}/outputs/lint-results.xml"
    }
}
